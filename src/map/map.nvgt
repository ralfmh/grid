#include "block.nvgt"
#include "definition.nvgt"
#include "entity.nvgt"

/*
** Represents a 2-dimensional map of tiles and text areas.
*/
final class Map
{
  private grid<int>@ blocks;  // Grid of blocks.
  
  private int blocksMaxX; // The maximum x position of the blocks on this map.
  
  private int blocksMaxY; // The maximum y position of the blocks on this map.
  
  private string definition;  // The complete map definition.
  
  private Entity@[] entities; // Array of entities.
  
  private grid<int>@ texts; // Grid of texts.
  
  private int textMaxX; // The maximum x position of the texts on this map.
  
  private int textMaxY; // The maximum y position of the texts on this map.

  /*
  ** Returns the maximum x position of all blocks on the map.
  */
  int GetMaxX()
  {
    return this.blocksMaxX;
  }
  
  /*
  ** Parses a map definition, fills the entities array and sets
  ** the maximum x and y positions of the map.
  ** Returns an array of validation errors that occurred
  ** while parsing the definition.
  */
  string[] Parse(
    string definition // The complete map definition.
  )
  {
    Entity@ entity;
    string label;
    string[] processedSegments;
    int requiredIndex;
    string[] requiredSegments;
    int typeIndex;
    string[] validationErrors;
    string value;
    this.definition = definition;
    @this.blocks = null;
    @this.texts = null;
    this.entities.resize(0);
    this.blocksMaxX = 0;
    this.blocksMaxY = 0;
    this.textMaxX = 0;
    this.textMaxY = 0;
    if (definition.empty())
    {
      validationErrors.insert_last("Map is empty.");
      return validationErrors;
    }
    string[] lines = definition.split(LINE_SEPARATOR);
    string[] segments;
    bool lineHasErrors;
    for (int l = 0; l < lines.length(); l++)
    {
      lineHasErrors = false;
      segments = lines[l].split(SEGMENT_SEPARATOR);
      typeIndex = -1;
      for (int t = 0; t < MAP_DEFINITION.length(); t++)
      {
        if (MAP_DEFINITION[t][0] == segments[0])
        {
          typeIndex = t;
          break;
        }
      }
      if (typeIndex == -1)
      {
        validationErrors.insert_last("Unknown type '" + segments[0]
          + "' in line " + (l + 1));
        lineHasErrors = true;
        continue;
      }
      @entity = Entity();
      entity.Type = EntityType(ENTITY_TYPE.find(MAP_DEFINITION[typeIndex][0]));
      processedSegments.resize(0);
      requiredSegments = GetRequiredSegments(typeIndex);
      for (int s = 1; s < segments.length(); s++)
      {
        label = segments[s].substr(0, 2);
        if (MAP_DEFINITION[typeIndex].find(1, label) == -1)
        {
          validationErrors.insert_last("Segment '" + label
            + "' is not supported for type '" + MAP_DEFINITION[typeIndex][0]
            + "' in line " + (l + 1));
          lineHasErrors = true;
          break;
        }
        if (processedSegments.find(label) > -1)
        {
          validationErrors.insert_last("Duplicate segment '" + label
            + "' in line " + (l + 1));
          lineHasErrors = true;
        }
        requiredIndex = requiredSegments.find(label);
        if (requiredIndex > -1)
        {
          requiredSegments.remove_at(requiredIndex);
        }
        processedSegments.insert_last(label);
        value = segments[s].substr(2);
        if (value.empty())
        {
          validationErrors.insert_last("The value for segment '" + label
            + "' is missing in line " + (l + 1));
          lineHasErrors = true;
          break;
        }
        if (label != SEGMENT_LV && label != SEGMENT_ML
          && !value.is_digits())
        {
          validationErrors.insert_last("Invalid value '" + value
            + "' for segment '" + label + "' in line " + (l + 1));
          lineHasErrors = true;
          break;
        }
        if (label == SEGMENT_FX)
        {
          entity.FromX = parse_int(value);
        }
        else if (label == SEGMENT_FY)
        {
          entity.FromY = parse_int(value);
        }
        else if (label == SEGMENT_ML)
        {
          entity.Content = value;
        }
        else if (label == SEGMENT_TX)
        {
          entity.ToX = parse_int(value);
        }
        else if (label == SEGMENT_TY)
        {
          entity.ToY = parse_int(value);
        }
        else if (label == SEGMENT_LV)
        {
          entity.Content = lines[l].substr(lines[l].find_first(SEGMENT_LV) + 2);
          break;
        }
      }
      if (!lineHasErrors && requiredSegments.length() > 0)
      {
        string message = "The following segments are missing for type '"
          + MAP_DEFINITION[typeIndex][0] + "' in line " + (l + 1) + ": ";
        for (int i = 0; i < requiredSegments.length(); i++)
        {
          if (i > 0)
          {
            message += ", ";
          }
          message += requiredSegments[i];
        }
        validationErrors.insert_last(message);
        lineHasErrors = true;
      }
      if (lineHasErrors)
      {
        continue;
      }
      if (entity.ToX < entity.FromX)
      {
        validationErrors.insert_last("'tx' may not be less than 'fx' in line "
          + (l + 1));
        break;
      }
      if (entity.ToY < entity.FromY)
      {
        validationErrors.insert_last("'ty' may not be less than 'fy' in line "
          + (l + 1));
        break;
      }
      if (entity.Type == TYPE_TILE)
      {
        entity.ToY = entity.FromY;
      }
      this.entities.insert_last(entity);
      if (entity.Type == Text)
      {
        if (entity.ToX > this.textMaxX)
        {
          this.textMaxX = entity.ToX;
        }
        if (entity.ToY > this.textMaxY)
        {
          this.textMaxY = entity.ToY;
        }
      }
      else
      {
        if (entity.ToX > this.blocksMaxX)
        {
          this.blocksMaxX = entity.ToX;
        }
        if (entity.ToY > this.blocksMaxY)
        {
          this.blocksMaxY = entity.ToY;
        }
      }
    }
    return validationErrors;
  }
  
  /*
  ** Creates the blocks and texts grids.
  */
  void CreateGrids()
  {
    @this.blocks = grid<int>(this.blocksMaxX + 1, this.blocksMaxY + 1);
    for (int i = 0; i <= this.blocksMaxX; i++)
    {
      for (int j = 0; j <= this.blocksMaxY; j++)
      {
        this.blocks[i, j] = -1;
      }
    }
    @this.texts = grid<int>(this.textMaxX + 1, this.textMaxY + 1);
    for (int i = 0; i <= this.textMaxX; i++)
    {
      for (int j = 0; j <= this.textMaxY; j++)
      {
        this.texts[i, j] = -1;
      }
    }
    for (int i = 0; i < this.entities.length(); i++)
    {
      if (this.entities[i].Type == Text)
      {
        for (int x = this.entities[i].FromX; x <= this.entities[i].ToX; x++)
        {
          for (int y = this.entities[i].FromY; y <= this.entities[i].ToY; y++)
          {
            this.texts[x, y] = i;
          }
        }
      }
      else
      {
        for (int x = this.entities[i].FromX; x <= this.entities[i].ToX; x++)
        {
          for (int y = this.entities[i].FromY; y <= this.entities[i].ToY; y++)
          {
            this.blocks[x, y] = i;
          }
        }
      }
    }
  }
  
  /*
  ** Gets the block entity at a position.
  ** Returns null if none exists at the given position,
  ** or the position lies outside of map boundaries.
  */
  Block@ GetBlock(
    int x,  // The x position.
    int y // The y position.
  )
  {
    if (x >= this.blocks.width() || y >= this.blocks.height()
      || this.blocks[x, y] == -1)
    {
      return null;
    }
    return Block(this.entities[this.blocks[x, y]].Content,
      this.entities[this.blocks[x, y]].Type);
  }
  
  /*
  ** Gets the text at a position.
  ** Returns the default text if none exists at the given position,
  ** or the position lies outside of map boundaries.
  */
  string GetText(
    int x,  // The x position.
    int y // The y position.
  )
  {
    if (x >= this.texts.width() || y >= this.texts.height()
      || this.texts[x, y] == -1)
    {
      return DEFAULT_TEXT;
    }
    return this.entities[this.texts[x, y]].Content;
  }
}