#include "../map/map.nvgt"
#include "direction.nvgt"
#include "move_result.nvgt"

/*
** Represents a person moving around the map.
*/
final class Person
{
  Block@ Block; // The current block at which the person is located.
  
  private int health; // Current health of the person.
  
  private Map@ map; // The current map.
  
  int X;  // Current x position.
  
  int Y;  // Current y position.
  
  /*
  ** Initializes a new instance of the Person class.
  */
  Person(
    int initialX, // The initial x position.
    int initialY, // The initial y position.
    Map@ map  // The current map.
  )
  {
    @this.Block = null;
    this.health = 100;
    @this.map = @map;
    this.X = initialX;
    this.Y = initialY;
  }
  
  /*
  ** Moves the person in the given direction.
  ** Returns MoveResult.Moved if the move succeeded.
  */
  MoveResult Move(
    Direction direction // The direction in which to move.
  )
  {
    int newX = this.X;
    int newY = this.Y;
    switch (direction)
    {
      case Down:
        newY--;
        break;
      case Left:
        newX--;
        break;
      case Right:
        newX++;
        break;
      case Up:
        newY++;
        break;
    }
    if (newX > this.map.GetMaxX() || newX < MAP_MIN_X)
    {
      return Edged;
    }
    @this.Block = this.map.GetBlock(newX, newY);
    if (this.Block == null || this.Block.Type != Wall)
    {
      this.X = newX;
      this.Y = newY;
      return Moved;
    }
    return Walled;
  }
}