#include "../map/map.nvgt"
#include "auto_move_state.nvgt"
#include "block_searcher.nvgt"
#include "constants.nvgt"
#include "direction.nvgt"
#include "move_result.nvgt"

/*
** Represents a person moving around the map.
*/
class Person
{
  /*
  ** The interval in ms for each automatically performed move.
  */
  private int autoMoveInterval;

  AutoMoveState AutoMoveState;  // Current AutoMoveState.

  private timer autoMoveTimer;  // The timer controlling automatic moves.

  /*
  ** The current block at which the person is located. It may include walls
  ** or empty space.
  */
  Block@ Block;

  int FallInterval; // The interval in ms for each falling move.

  int Health; // Current health of the person.

  /*
  ** Boolean value indicating whether the person is hooked to a block.
  */
  bool Hooked;

  /*
  ** The block that the person is standing on while hooked to another block.
  */
  Block HookBlock("rope", Tile);

  private int hookFromY;  // The minimum y position of the hook.

  private int hookToY;  // The maximum y position of the hook.

  int JumpInterval; // The interval in ms for each jumping move.

  private int jumpStart;  // Y position where the person started to jump.

  /*
  ** The last known material that the person was standing on.
  */
  string LocalMaterial;

  private Map@ map; // The current map.

  /*
  ** The interval in ms for each manually performed move.
  */
  int ManualMoveInterval;

  private timer manualMoveTimer;  // The timer controlling manual moves.

  /*
  ** The last known text that was associated with the person's position.
  */
  private string text;

  int X;  // Current x position.

  int Y;  // Current y position.

  /*
  ** Initializes a new instance of the Person class.
  */
  Person(
    int initialX, // The initial x position.
    int initialY, // The initial y position.
    Map@ map  // The current map.
  )
  {
    this.AutoMoveState = Standing;
    this.Health = 100;
    @this.map = @map;
    this.X = initialX;
    this.Y = initialY;
    @this.Block = this.map.GetBlock(this.X, this.Y);
    this.LocalMaterial = this.Block != null ? this.Block.Material
                         : this.map.DefaultMaterial;
  }

  /*
  ** Moves the person automatically (e.g during jumping or falling).
  ** Returns true if the AutoMoveState changed, false otherwise.
  */
  bool AutoMove()
  {
    MoveResult result;
    switch (this.AutoMoveState)
    {
      case Jumping:
        result = this.Move(Up, true);
        if ((this.Y - this.jumpStart) >= DEFAULT_JUMP_RANGE
            || result == Walled)
        {
          this.AutoMoveState = Landing;
          this.autoMoveInterval = this.FallInterval;
          return true;
        }
        return false;
      case Landing:
        if (this.Y <= this.jumpStart)
        {
          this.Fall();
          return true;
        }
      case Falling:
        @this.Block = this.map.GetBlock(this.X, this.Y);
        if (this.Block != null)
        {
          this.AutoMoveState = Standing;
          return true;
        }
        result = this.Move(Down, true);
        if ((result != Delayed && this.Block != null) || result == Edged)
        {
          this.AutoMoveState = Standing;
          return true;
        }
        return false;;
    }
    return false;
  }

  /*
  ** Initiates a jump from the current position.
  ** Returns true if the jump was initiated, false otherwise.
  */
  bool Jump()
  {
    if (this.AutoMoveState != Standing)
    {
      return false;
    }
    this.jumpStart = this.Y;
    this.AutoMoveState = Jumping;
    this.autoMoveInterval = this.JumpInterval;
    return true;
  }

  /*
  ** Starts falling from the current position.
  */
  private void Fall()
  {
    this.AutoMoveState = Falling;
    this.autoMoveInterval = this.FallInterval;
  }

  /*
  ** Moves the person in the given direction.
  ** Returns MoveResult.Moved if the move succeeded.
  */
  MoveResult Move(
    Direction direction // The direction in which to move.
  )
  {
    return this.Move(direction, false);
  }

  /*
  ** Changes the persons position, setting X, Y and setting sound_listener.
  ** Returns boolean if the move succeeded.
  */
  void ChangePosition(
    int x, // .
    int y // .
  )
  {
    this.X=x;
    this.Y=y;
    sound_set_listener_position(this.X,this.Y,0); //Sets the sound listener position.
  }

  /*
  ** Moves the person in the given direction.
  ** Returns MoveResult.Moved if the move succeeded.
  */
  private MoveResult Move(
    Direction direction,  // The direction in which to move.
    bool isAutoMove // Boolean value indicating whether the move is performed automatically.
  )
  {
    if (isAutoMove)
    {
      if (this.autoMoveTimer.elapsed < this.autoMoveInterval)
      {
        return Delayed;
      }
      this.autoMoveTimer.restart();
    }
    else
    {
      if (this.manualMoveTimer.elapsed < this.ManualMoveInterval)
      {
        return Delayed;
      }
      this.manualMoveTimer.restart();
    }
    int newX = this.X;
    int newY = this.Y;
    switch (direction)
    {
      case Down:
        newY--;
        break;
      case Left:
        newX--;
        break;
      case Right:
        newX++;
        break;
      case Up:
        newY++;
        break;
    }
    if (newX > this.map.GetMaxX() || newX < MAP_MIN_X || newY < MAP_MIN_Y)
    {
      return Edged;
    }
    if (this.Hooked)
    {
      if (newY <= this.hookFromY || newY >= this.hookToY || newX != this.X)
      {
        this.Hooked = false;
        @this.Block = this.map.GetBlock(newX, newY);
        this.ChangePosition(newX,newY);
        this.Fall();
      }
      else
      {
        @this.Block = @this.HookBlock;
      }
    }
    else
    {
      @this.Block = this.map.GetBlock(newX, newY);
    }
    if (this.Block == null)
    {
      if (this.Hooked)
      {
        this.ChangePosition(newX,newY);
        return Moved;
      }
      else if (this.AutoMoveState == Standing && newY == this.Y)
      {
        this.Fall();
      }
      else if (!isAutoMove && newY != this.Y)
      {
        return Edged;
      }
    }
    else if (this.Block.Type == Wall)
    {
      if (isAutoMove
          && (this.AutoMoveState == Landing || this.AutoMoveState == Falling))
      {
        this.ChangePosition(newX,newY);
        this.LocalMaterial = this.Block.Material;
      }
      return Walled;
    }
    this.ChangePosition(newX,newY);
    this.LocalMaterial = this.Block != null ? this.Block.Material
                         : this.map.DefaultMaterial;
    return Moved;
  }

  /*
  ** Checks if a block with a different material than the current block
  ** is approached during movement.
  */
  bool Sonar(
    Direction direction,  // The direction in which to search for the block.
    vector &out position,  // The position of the block.
    EntityType &out type  // The type of the block.
  )
  {
    string remoteMaterial;
    BlockSearcher searcher(direction, this.map, this.X, this.Y,
                           DEFAULT_SONAR_RANGE, DEFAULT_SONAR_RANGE);
    Block@ block;
    while(searcher.Find(block))
    {
      if (@block != null)
      {
        remoteMaterial = block.Material;
        type = block.Type;
      }
      else
      {
        remoteMaterial = this.map.DefaultMaterial;
        type = None;
      }
      if (remoteMaterial != this.LocalMaterial
          && (searcher.SearchAxis == Horizontal
              || remoteMaterial != this.map.DefaultMaterial))
      {
        position = searcher.GetPosition();
        return true;
      }
    }
    return false;
  }

  /*
  ** Summarizes the horizontally or vertically adjacent blocks.
  */
  string View(
    Direction direction // The direction in which to view the adjacent blocks.
  )
  {
    string view = "";
    Block@ block;
    string viewMaterial = this.LocalMaterial;
    string remoteMaterial;
    string type;
    BlockSearcher searcher(direction, this.map, this.X, this.Y,
                           DEFAULT_VIEW_HORIZONTAL, DEFAULT_VIEW_VERTICAL);
    while (searcher.Find(block))
    {
      type = "";
      if (@block != null)
      {
        remoteMaterial = block.Material;
        if (block.Type != Tile)
        {
          type = ENTITY_TYPE_NAME[block.Type] + " ";
        }
      }
      else
      {
        remoteMaterial = this.map.DefaultMaterial;
      }
      if (remoteMaterial != viewMaterial)
      {
        if (searcher.SearchAxis == Horizontal
            || remoteMaterial != this.map.DefaultMaterial)
        {
          view += type + remoteMaterial + " (" + searcher.Squares + "), ";
        }
        viewMaterial = remoteMaterial;
      }
    }
    if (view == "")
    {
      return "nothing";
    }
    return view;
  }

  /*
  ** Hooks into a block above or below the current position.
  */
  void Hook(
    Direction direction,  // The vertical direction in which to hook.
    int xOffset // The offset from the x axis.
  )
  {
    if (this.Hooked)
    {
      this.Hooked = false;
      this.Fall();
      @this.Block = null;
      return;
    }
    int x = this.X + xOffset;
    if (x < MAP_MIN_X || x > this.map.GetMaxX())
    {
      return;
    }
    Block@ block;
    int fromY;
    int toY;
    BlockSearcher searcher(direction, this.map, x, this.Y,
                           0, DEFAULT_HOOK_RANGE);
    while (searcher.Find(block, fromY, toY))
    {
      if (@block != null)
      {
        this.Hooked = true;
        this.AutoMoveState = Standing;
        if (direction == Up)
        {
          this.Y++;
          this.hookFromY = this.Y;
          this.hookToY = toY;
        }
        else
        {
          this.Y--;
          this.hookFromY = fromY;
          this.hookToY = this.Y;
        }
        this.X = x;
        return;
      }
    }
  }

  /*
  ** Gets the text that is associated with the person's position on the map.
  ** Returns true if the text has changed since the last call, false otherwise.
  */
  bool GetText(
    string &out text  // The current text.
  )
  {
    text = this.map.GetText(this.X, this.Y);
    bool changed = text != this.text;
    this.text = text;
    return changed;
  }
}